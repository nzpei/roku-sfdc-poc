function Main(args as object) as void
    screen = CreateObject("roSGScreen")
    port = CreateObject("roMessagePort")
    screen.setMessagePort(port)
    m.global = screen.getGlobalNode()
    Logger.Init()

    Registry.ClearSfdcLibUrlsIfNeeded(args)

    scene = screen.CreateScene("BootstrapScene")
    screen.show()

    scene.ObserveField("exitChannel", port)
    scene.launchArgs = args

    if false
        ' These vars are needed to pass certification.
        ' The actual work is done in Lib
        ' bs:disable-next-line LINT1005
        contentId = args.contentId
        ' bs:disable-next-line LINT1005
        mediaType = args.mediaType

        ' Do not signal app launch complete here, we're doing it over at Lib
        ' This is needed to pass cetification
        scene.signalBeacon("AppLaunchComplete")
    end if

    input = CreateObject("roInput")
    input.setMessagePort(port)

    ' TODO:P2 follow up on bug where roSystemLog does not fire for a video player in ComponentLibrary
    ' https://community.roku.com/t5/Roku-Developer-Program/BUG-roSystemLog-not-firing-with-ComponentLibrary/td-p/906075
    systemLog = CreateObject("roSystemLog")
    systemLog.SetMessagePort(port)
    systemLog.EnableType("http.connect")
    systemLog.EnableType("http.error")
    systemLog.EnableType("http.complete")
    systemLog.EnableType("bandwidth.minute")

    #if DEBUG
        ' The following comment is to enable the SceneGraph inspector
        ' on the VSCode BrightScript plugin.
        ' vscode_rdb_on_device_component_entry

        ' The following comment is to enable RALE tracking
        ' vscode_rale_tracker_entry
    #end if

    while true
        msg = wait(0, port)
        msgType = type(msg)
        if msgType = "roSystemLogEvent"
            info = msg.GetInfo()
            info.DateTime = info.DateTime.ToISOString()
            scene.systemLogEvent = info
        else if msgType = "roSGScreenEvent"
            if msg.isScreenClosed()
                return
            end if
        else if msgType = "roSGNodeEvent"
            field = msg.getField()
            data = msg.getData()
            if field = "exitChannel" and data = true
                return
            end if
        else if msgType = "roInputEvent"
            scene.inputArgs = msg.getInfo()
        end if
    end while

end function
