namespace Tests

    @suite
    class TimeUtilsTests extends Rooibos.BaseTestSuite

        @describe("TimeUtils")

        @it("formats the time correctly")
        @params(invalid, "0:00")
        @params(-1, "0:00")
        @params(0, "0:00")
        @params(7, "0:07")
        @params(138, "2:18")
        @params(8285, "2:18:05")
        @params(1282085, "356:08:05")
        function _(length as dynamic, formattedString as string)
            m.assertEqual(TimeUtils.GetFormattedTime(length), formattedString)
        end function

        @it("formats the time left correctly")
        @params(invalid, "N/A")
        @params(-1, "now")
        @params(0, "now")
        @params(1, "1 second")
        @params(7, "7 seconds")
        @params(8285, "2 hours")
        @params(1282085, "15 days")
        function _(length as dynamic, formattedString as string)
            time = invalid
            if length <> invalid
                time = TimeUtils.Now().AsSeconds() + length
            end if
            m.assertEqual(TimeUtils.GetFormattedTimeLeft(time), formattedString)
        end function

    end class

end namespace
